#MIN_CONFIG
AC_PREREQ(2.50)
AC_LANG_CPLUSPLUS
AM_INIT_AUTOMAKE(sim,0.9.4)
AC_PROG_LIBTOOL
AC_STDC_HEADERS
AC_C_BIGENDIAN
KDE_NEED_FLEX
AM_CONDITIONAL(QT2, test "$kde_qtver" = "2")
if test "$kde_qtver" = "2" ; then
 LIBUIQT="../../sim/qt3/libsimuiqt.la"
 LIBUI="../../sim/qt3/libsimuiqt.la"
else
 LIBUIQT=""
 LIBUI=""
fi
AC_SUBST(LIBUIQT)
AC_SUBST(LIBUI)
LFLAGS="-o${LEX_OUTPUT_ROOT}.c"
AC_SUBST(LFLAGS)
if test "$CYGWIN" = "yes"; then
 LDFLAGS="$LDFLAGS --enable-auto-import"
fi
if ! echo $CXXFLAGS | fgrep '-fno-strict-aliasing' >/dev/null 2>&1 ; then 
	old_cxxflags=$CXXFLAGS 
    CXXFLAGS="$CXXFLAGS -fno-strict-aliasing" 
    AC_TRY_COMPILE([], [return 0;], , CXXFLAGS=$old_cxxflags) 
fi 
RELEASE_BUILD="no"
AC_ARG_ENABLE(
 release,
 [  --enable-release        enable release build],
 RELEASE_BUILD="$enableval")
if test "$RELEASE_BUILD" != "yes"; then
 AC_DEFINE(CVS_BUILD, 1, [CVS build])
fi

AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_UID_T
AC_STRUCT_TM
AC_PATH_X
AC_HEADER_SYS_WAIT
AC_TM_GMTOFF
AC_FUNC_FORK
AC_C_INLINE
AC_C_CONST
KDE_CHECK_SSL
AC_HEADER_STDBOOL
AC_FUNC_ERROR_AT_LINE
AC_FUNC_MMAP
AC_FUNC_MKTIME
AC_FUNC_MEMCMP
AC_FUNC_MALLOC
AC_FUNC_REALLOC
AC_FUNC_STAT

AC_LIB_LTDL

AC_CHECK_HEADERS([arpa/inet.h netinet/in.h sys/ioctl.h sys/socket.h])
AC_CHECK_HEADERS([fcntl.h limits.h netdb.h stddef.h termios.h])

AC_CHECK_FUNCS([dup2 gethostbyname gethostname inet_ntoa])
AC_CHECK_FUNCS([memset mkdir munmap rmdir socket])
AC_CHECK_FUNCS([strcspn strdup strerror strstr strtol strtoul])

AC_CHECK_FUNC(strcasecmp,[AC_DEFINE(HAVE_STRCASECMP,1,[Have strcasecmp])])
AC_CHECK_FUNC(chmod,[AC_DEFINE(HAVE_CHMOD,1,[Have chmod])])

AC_EGREP_HEADER(in_addr_t, netinet/in.h,, AC_DEFINE(in_addr_t,int,[defaults in_addr_t to int]))
AC_EGREP_HEADER(socklen_t, sys/socket.h,, AC_DEFINE(socklen_t,int,[defaults socklen_t to int]))

AC_CHECK_LIB(xnet,inet_addr)
AC_CHECK_LIB(socket, socket)

AC_MSG_CHECKING([for The Ultimate Answer])
AC_MSG_RESULT([42])

EXTRA_LIBS=""
if test "$use_kde" = "yes" ; then
KDE_CHECK_HEADER(ktextedit.h,[AC_DEFINE(HAVE_KTEXTEDIT_H,1,[Have ktextedit.h])])
fi
KDE_CHECK_HEADER(qsyntaxhighlighter.h,[AC_DEFINE(HAVE_QSYNTAXHIGHLIGHTER_H,1,[Have qsyntaxhighlighter.h])])
AC_DEFINE_UNQUOTED(PREFIX, "$prefix", [Install prefix])
AC_SUBST(EXTRA_LIBS)
if test "${exec_prefix}" = "NONE" ; then
  exec_prefix=$prefix
fi
libdir=`eval echo $libdir`

sim_plugindir="${libdir}/sim"
sim_stylesdir="\${sim_plugindir}/styles"
AC_SUBST(sim_plugindir)
AC_SUBST(sim_stylesdir)
AC_DEFINE_UNQUOTED(PLUGIN_PATH,"${sim_plugindir}",[Plugins path])

LIB_XML2=""
AC_PATH_PROG(XML2_CONFIG, xml2-config, no)
if test "x$XML2_CONFIG" = "xno" ; then
   AC_MSG_ERROR([Executable xml2-config not found!
Seems you have no libxml2 devel files installed.
You can download it from http://xmlsoft.org/download.html
])
fi

LIB_XML2=`$XML2_CONFIG --libs 2>/dev/null`
CPPFLAGS="$CPPFLAGS `$XML2_CONFIG --cflags`"

LIB_XSLT=""
AC_PATH_PROG(XSLT_CONFIG, xslt-config, no)
if test "x$XSLT_CONFIG" = "xno" ; then
   AC_MSG_ERROR([Executable xslt-config not found!
Seems you have no libxslt devel files installed.
You can download it from http://xmlsoft.org/download.html
])
fi

LIB_XSLT=`$XSLT_CONFIG --libs 2>/dev/null`
CPPFLAGS="$CPPFLAGS `$XSLT_CONFIG --cflags`"

AC_SUBST(LIB_XML2)
AC_SUBST(LIB_XSLT)

ZIP_FLAGS=""
AC_PATH_PROG(ZIP, zip, no)
if test "x$ZIP" = "xno" ; then
   AC_PATH_PROG(ZIP, jar, no)
   ZIP_FLAGS=" -cMf "
   if test "x$ZIP" = "xno" ; then
     AC_MSG_ERROR([Executable zip or jar not found!
])
   fi
fi
AC_SUBST(ZIP)
AC_SUBST(ZIP_FLAGS)

uname=`uname`
unamem=`uname -m`
AC_DEFINE_UNQUOTED(UNAME,"${uname} ${unamem}",[System type])

if test "$have_ssl" != yes; then
  echo ""
  echo "OpenSSL support is not available.  SIM will not be able to"
  echo "establish secure communication channels. This is not required"
  echo "for proper function (i.e. it is optional). If you really desire this"
  echo "functionality then install OpenSSL (libraries and headers) and"
  echo "rebuild SIM.  OpenSSL can be found at http://www.openssl.org"
  echo "or as RPM from your local distribution site."
fi

AM_CONDITIONAL(QT_WIN, test "$kde_use_qt_win" = "yes")

if test "$kde_use_qt_win" = "yes"; then
   AC_SUBST(SIM_RC_OBJ, "sim_rc.o")
fi

DO_NOT_COMPILE="$DO_NOT_COMPILE plugins/update"
